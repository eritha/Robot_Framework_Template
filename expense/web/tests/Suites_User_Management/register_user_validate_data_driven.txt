*** Settings ***
Documentation     /* Suite Register User Validation in Alternative flows (A)*/
Suite Setup       Open Browser And Go To Register Page    ${sever_test}
Suite Teardown    Close All Browsers
Test Template     Register New User Validation
Resource          ../../conf/selenium_connect.txt
Library           Selenium2Library

*** Variables ***
${acc_valid}      expensetest@mailinator.com
${acc_invalid_long}    thisIsAVeryLongEmail_thisIsAVeryLongEmail_thisIsAVeryLongEmail_thisIsAVeryLongEmail@mailinator.com
${acc_inactive}    expenseaccinactive@mailinator.com
${acc_lock}       expenseacclock@mailinator.com
${valid_password}    5sSDioii"'s2/23%2@sd
${invalid_password}    abc123
${js_injection_email}    <button onclick=alert('Hi');>normalEC@mailinator.com</button>

*** Testcases ***     Username                 Password               RePassword                  Message Show
TC_Missing field in sign in information (A1) - (1-High)
                      [Tags]                   User Management        Register User Validation    Completed
                      ${EMPTY}                 ${EMPTY}               ${EMPTY}                    Email must be present.\nPassword must be present.

TC_Email syntax invalid (A2) - (1-High)
                      [Tags]                   User Management        Register User Validation    Completed
                      abc                      ${valid_password}      ${valid_password}           Enter a valid email.

TC_Domain of Email address too short (A3) - (1- Medium)
                      [Tags]                   User Management        Register User Validation    Completed
                      abc@def.c                ${valid_password}      ${valid_password}           Enter a valid email.

TC_Registration with email of existed account (A5) - (1-High)
                      [Tags]                   User Management        Register User Validation    Completed
                      ${user_owner}            ${EMPTY}               ${EMPTY}                    This email used by another user.

TC_Mismatching password (A6) - (1-High)
                      [Tags]                   User Management        Register User Validation    Completed
                      ${acc_valid}             abc12345               ABC12345                    Password must be a minimum of 10 characters long and must include at least 1 number.

TC_Email too long (A7) - (2-Medium)
                      [Tags]                   User Management        Register User Validation    Completed
                      ${acc_invalid_long}      ${valid_password}      ${valid_password}           Enter a valid email.

TC_SQL injection in text-boxes (A8) - (1-High)
                      [Tags]                   User Management        Register User Validation    Completed
                      'test@mailinator.com'    ${valid_password}      ${valid_password}           Enter a valid email.

TC_Javascript injection in text-boxes (A9) - (1-High)
                      [Tags]                   User Management        Register User Validation    Completed
                      ${js_injection_email}    ${valid_password}      ${valid_password}           Enter a valid email.

TC_Sign up using locked account (A15) - (2-Medium)
                      [Tags]                   User Management        Register User Validation    Completed
                      ${acc_lock}              ${valid_password}      ${valid_password}           This email used by another user.\nPasswords do not match.

TC_Password invalid (A16) - (1-High) - less than 10 characters
                      [Tags]                   User Management        Register User Validation    Completed
                      ${acc_valid}             ${invalid_password}    ${invalid_password}         Password must be a minimum of 10 characters long and must include at least 1 number.

TC_Password invalid (A16) - (1-High) - more than 10 characters but doesn't include 1 number
                      [Tags]                   User Management        Register User Validation    Completed
                      ${acc_valid}             abcdefabcdef           abcdefabcdef                Password must be a minimum of 10 characters long and must include at least 1 number.

*** Keywords ***
Register New User Validation
    [Arguments]    ${username}    ${password}    ${re_password}    ${msg_show}
    [Documentation]    SUITE_KEY: Input username & pass, then submit to verify message show
    Click Link    /users/register
    Input New User And Submit    ${username}    ${password}    ${re_password}
    Verify Message Error Show    ${msg_show}
